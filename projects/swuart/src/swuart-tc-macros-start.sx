
	;; -*- asm -*-

	;; Macros for handling start of frame:
	;;   * enabling/disabling falling edge detection on RXD;
	;;   * isr called on falling edge.

	;; The RXD pin name determines the macros to be used.

#define enable_start	HW_G2(enable_start, hw_name(SWUART_PIN_RX))
#define disable_start	HW_G2(disable_start, hw_name(SWUART_PIN_RX))

	;; Enable INT0 interrupt
	;; 
	.macro enable_start_int0 reg
	XCF	\reg, START_IF
	XSB	\reg, START_IE
	.endm

	;; Disable INT0 interrupt
	;; 
	.macro disable_start_int0 reg
	XCB	\reg, START_IE
	.endm


	;; Enable AIN1 interrupt
	;; 
	.macro enable_start_ain1 reg
#if ACSR < 0x20
	sbi	ACSR, ACI	; [2]	Clear flag
	sbi	ACSR, ACIE	; [2]	Set mask
	CYCLES=CYCLES+4
#else
	cli				; [1]
	in	\reg, ACSR		; [1]
	ori	\reg, 1<<ACI | 1<<ACIE	; [1]	Clear flag, set mask
	sei				; [1]
	out	ACSR, \reg		; [1]
	CYCLES=CYCLES+5
#endif
	.endm

	;; Disable AIN1 interrupt
	;; 
	.macro disable_start_ain1 reg
#if ACSR < 0x20
	cbi	ACSR, ACIE	; [2]	Clear mask
	CYCLES=CYCLES+2
#else
	cli			; [1]
	in	\reg, ACSR	; [1]
	andi	\reg, ~(1<<ACIE); [1]
	sei			; [1]
	out	ACSR, \reg	; [1]
	CYCLES=CYCLES+5
#endif
	.endm


	;; Enable PCIE1/PCINT8 interrupt
	;; 
	.macro enable_start_pin_pcint8 reg
#if GIFR < 0x20
	sbi	GIFR, PCIF1	; [2]	Clear flag
	CYCLES=CYCLES+2
#else
	ldi	\reg, 1<<PCIF1	; [1]
	out	GIFR, \reg	; [1]
	CYCLES=CYCLES+2
#endif
#if GIMSK < 0x20
	sbi	GIMSK, PCIE1	; [2]	Set mask
	CYCLES=CYCLES+2
#else
	cli			; [1]
	in	\reg, GIMSK	; [1]
	ori	\reg, 1<<PCIE1	; [1]
	sei			; [1]
	out	GIMSK, \reg	; [1]
	CYCLES=CYCLES+5
#endif
	.endm

	;; Disable PCIE1/PCINT8 interrupt
	;; 
	.macro disable_start_pin_pcint8 reg
#if GIMSK < 0x20
	cbi	GIMSK, PCIE1	; [2]
	CYCLES=CYCLES+2
#else
	cli			; [1]
	in	\reg, GIMSK	; [1]
	andi	\reg, ~(1<<PCIE1); [1]
	sei			; [1]
	out	GIMSK, \reg	; [1]
	CYCLES=CYCLES+5
#endif
	.endm
