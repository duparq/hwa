
	;; -*- asm -*-

	;; u16 sync_10_1 ( void )
	;; 
	;; Determines baudrate by measuring the low-level durations of 10 bits
	;; low / 1 bit low sequence.
	;; 
	;; Returns the number of 'syshz' cycles of 1 bit (R25:R24)
	;;
	;; Measures are done with units of 5 cycles. This means that the longest
	;; low-level duration must be < 327675 cycles.
	;;
	;; This routine can not synchronize at speeds below syshz/32767 bps
	;; using 10 low-level bits characters.
	;;
	;; FT232R seems to have a bug under Linux-3.0: at 1 kbps, random
	;; duration of 10 bits low!
	;; 
	;; GCC call-used registers: R18..R27, R30, R31 (these registers can be
	;; used freely by the callee)
	;;

	
	;; Synchronize UART :
	;;   Measure consecutive low-level durations: say t1 and t2.
	;;   When 9.5×t2 <= t1 <= 10.5×t2, synchronization is done
	;;   and R15:R14 contains the bit duration (baudrate) in CPU cycles.
	;;
	;; Durations are measured in units of 5 cycles, so duration of 10 bits
	;; in 5 cycle units gives the duration of 2 bits in CPU cycles.
	;; 
	;; Counts overflow at 65535 units, i.e. 65535*5 cycles
	;; That means that the longest low-level duration must be < 327675 cycles
	;; and this routine can not synchronize at speeds below SYSHZ/(327675/10) bps
	;; using 10 low-level bits characters, i.e.  bps @8 MHz.
	;; 
	;;   8 MHz / 1200 bps:
	;;    1 bit = 833µs =  6667 cycles = 1333 units
	;;    10 bits =  units
	;;    t1 = 13316, t2 = 1331, t = 6658
	;; 
	;;   8 MHz / 2400 bps
	;;    1 bit = 417µs = 3333 cycles = 667 units
	;;    10 bits = 6667 units
	;;    t1 = 6658, t2 = 682, t = 3329
	;;
	;;   8 MHz / 19200 bps
	;;    1 bit = 52.1µs = 417 cycles = 83 units
	;;    10 bits = 833 units
	;;    t1 = 832, t2 = 83, t = 416
	;;
	;;   8 MHz / 115200 bps
	;;    1 bit = 8.68µs = 69.4 cycles = 13.89 units
	;;    10 bits =  units
	;;    t1 = 138, t2 = 13, t = !

	.global		swuart_sync_10_1
	swuart_sync_10_1 = sync_10_1

	.section	.text.sync_10_1,"ax",@progbits
	
sync_10_1:

	;; Init last measure to 0
	;; 
	ldi	R24, 0
	ldi	R25, 0

	;; Shift measures
	;; 
1:	movw	R18, R24	;		t1 = t2
	ldi	R24, 0		;		t2 = 0
	ldi	R25, 0		;
	
	;; Measure next low-level duration in t2 (R25:R24)
	;; 
2:	sbis	RXDPIN, RXDBP	;		Wait RXD == 1
	rjmp	2b		;

2:	sbic	RXDPIN, RXDBP	; [1/2]		Wait RXD == 0
	rjmp	2b		; [2]

2:	adiw	R24, 1		; [2]	|	Compute low level duration
	sbis	RXDPIN, RXDBP	; [1/2]	|	in 5 CPU cycles units
	rjmp	2b		; [2]	|= 5

	;; Compute R27:R26 = 10×t2
	;; 
	movw	R26, R24	; R27:R26 = t2
	lsl	R26		;
	rol	R27		; R27:R26 = 2×t2
	lsl	R26		;
	rol	R27		; R27:R26 = 4×t2
	add	R26, R24	;
	adc	R27, R25	; R27:R26 = 5×t2
	lsl	R26		;
	rol	R27		; R27:R26 = 10×t2

	;; Compute R31:R30 = dt
	;; 
	movw	R30, R24	; R31:R30 = t2
	adiw	R30, 1		; round
	lsr	R31		;
	ror	R30		; R31:R30 = t2/2
	
	;; Compute R27:R26 = 10×t2 - dt
	;; 
	sub	R26, R30	;
	sbc	R27, R31	; R27:R26 = 10×t2 - dt

	;; Check low end
	;; 
	cp	R18, R26	; if ( t1 < 10×t2 - dt )
	cpc	R19, R27	;   then t1 is too low
	brcs	1b		;   loop to get another sample

	;; Compute R27:R26 = 10×t2 + dt
	;; 
	add	R26, R30	;
	adc	R27, R31	; R27:R26 = 10×t2
	add	R26, R30	;
	adc	R27, R31	; R27:R26 = 10×t2 + dt

	;; Check high end
	;; 
	cp	R18, R26	; if ( t1 > 10×t2 + dt )
	cpc	R19, R27	;   then t1 is too high
	brcc	1b		;   loop to get another sample

	;; Store result
	;; 
	movw	R24, R18	; R25:R24 = t1
	adiw	R24, 1		; round
	lsr	R25
	ror	R24		; R25:R24 = t1/2 = bit duration
